#Область ПрограммныйИнтерфейс

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Анкета клиента
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента";
	КомандаПечати.Представление = НСтр("ru = 'Анкета Клиента'");
	КомандаПечати.Порядок = 5;
	
	// Транспортная накладная
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Транспортная накладная'");
	КомандаПечати.Порядок = 10;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АнкетаКлиента,ТранспортнаяНакладная";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.Порядок = 100;

	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "УправлениеПечатью";
	КомандаПечати.Идентификатор = "Документ.ИЗМ_Доставка.ПФ_DOC_ДоговорПеревозки";
	КомандаПечати.Представление = НСтр("ru = 'Договор перевозки груза(DOCX)'");
	КомандаПечати.Порядок = 150;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АнкетаКлиента");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАнкеты(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Анкета Клиента'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ИЗМ_Доставка.ПФ_MXL_АнкетаКлиента";
	КонецЕсли;
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ТранспортнаяНакладная");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Транспортная накладная'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.ИЗМ_Доставка.ПФ_MXL_ТранспортнаяНакладная";
	КонецЕсли;

КонецПроцедуры

#Иначе
	
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");

#КонецЕсли

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПечатьАнкеты(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_АнкетаКлиента";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИЗМ_Доставка.ПФ_MXL_АнкетаКлиента");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокАнкеты(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиТабличнуюЧастьАнкеты(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьТранспортнойНакладной(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ТранспортнаяНакладная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ИЗМ_Доставка.ПФ_MXL_ТранспортнаяНакладная");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиТабличнуюЧастьНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		ВывестиПодвалНакладной(ДанныеДокументов, ТабличныйДокумент, Макет);

		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции


Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ИЗМ_Доставка.Номер КАК Номер,
	               |	ИЗМ_Доставка.Дата КАК Дата,
	               |	ИЗМ_Доставка.Ссылка КАК Ссылка,
	               |	ИЗМ_Доставка.Организация КАК Организация,
	               |	ИЗМ_Доставка.Контрагент КАК Контрагент,
	               |	ИЗМ_Доставка.Товары.(
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество
	               |	) КАК Товары
	               |ИЗ
	               |	Документ.ИЗМ_Доставка КАК ИЗМ_Доставка
	               |ГДЕ
	               |	ИЗМ_Доставка.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ВывестиЗаголовокАнкеты(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовокДокумента = Макет.ПолучитьОбласть("Заголовок");
	
	ДанныеПечати = Новый Структура;
	
	ШаблонЗаголовка = "Анкета доставки №%1 от %2 г.";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=D"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ОбластьЗаголовокДокумента.Параметры.Заполнить(ДанныеПечати);
	
	СтрокаСсылки = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеКода = ГенерацияШтрихкода.ДанныеQRКода(СтрокаСсылки, 1, 120);
	КартинкаКода = Новый Картинка(ДанныеКода);
	ОбластьЗаголовокДокумента.Рисунки.QRКод.Картинка = КартинкаКода;
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
	
	
	
КонецПроцедуры

Процедура ВывестиЗаголовокНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовокДокумента = Макет.ПолучитьОбласть("Заголовок");
	
	ДанныеПечати = Новый Структура;
	
	ШаблонЗаголовка = "Транспортная накладная №%1 от %2 г.";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=D"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	
	ОбластьЗаголовокДокумента.Параметры.Заполнить(ДанныеПечати);
    ОбластьЗаголовокДокумента.Параметры.Организация = ДанныеДокументов.Организация;
	ОбластьЗаголовокДокумента.Параметры.Контрагент = ДанныеДокументов.Контрагент;
	
	СтрокаСсылки = ПолучитьНавигационнуюСсылку(ДанныеДокументов.Ссылка);
	ДанныеКода = ГенерацияШтрихкода.ДанныеQRКода(СтрокаСсылки, 1, 120);
	КартинкаКода = Новый Картинка(ДанныеКода);
	ОбластьЗаголовокДокумента.Рисунки.QRКод.Картинка = КартинкаКода;
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовокДокумента);
	
	
	
КонецПроцедуры


Процедура ВывестиТабличнуюЧастьАнкеты(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьТаблицы = Макет.ПолучитьОбласть("Таблица");
	
	ТабличныйДокумент.Вывести(ОбластьТаблицы);
	
КонецПроцедуры

Процедура ВывестиТабличнуюЧастьНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока = Макет.ПолучитьОбласть("СтрокиТаблицы");
	
	ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
	
	ВыборкаТовары = ДанныеДокументов.Товары.Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		ОбластьСтрока.Параметры.Заполнить(ВыборкаТовары);
		ТабличныйДокумент.Вывести(ОбластьСтрока);
	КонецЦикла;
		
КонецПроцедуры

Процедура ВывестиПодвалНакладной(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьПодвалДокумента = Макет.ПолучитьОбласть("Подвал");
	
	ТабличныйДокумент.Вывести(ОбластьПодвалДокумента);
	
	
	
КонецПроцедуры

#Иначе
	
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");

#КонецЕсли

#КонецОбласти












